/*
    Password Manager: Manages accounts given by user with encrypted password.
    Copyright (C) 2022-2025  Francesco Marras (2004marras@gmail.com)

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see https://www.gnu.org/licenses/gpl-3.0.html.
 */

plugins {
    id 'java'
    // id 'java-library'
    id 'application'

    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'io.franzbecker.gradle-lombok' version '5.0.0'

    // https://github.com/gradlex-org/java-module-dependencies
    id 'org.gradlex.java-module-dependencies' version '1.11'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

mainModuleInfo {
    annotationProcessor 'lombok'
}

testModuleInfo {
    annotationProcessor 'lombok'
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    sourceCompatibility = JavaVersion.VERSION_25
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

application {
    // Define the main class for the application.
    mainModule = 'password.manager.lib'
    mainClass = 'password.manager.lib.Main'

    // JVM arguments
    applicationDefaultJvmArgs = [
        '--enable-native-access=javafx.graphics'
    ]
}

testing {
    suites {
        test {
            useJUnitJupiter '5.13.4'
        }
    }
}

javafx {
    version = '25.0.1'
    modules = [ 'javafx.base', 'javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.web' ]
}

jar {
    archiveBaseName.set(rootProject.name + 'Components')
    manifest {
        attributes 'Implementation-Title': 'Custom components for ' + project.appName
    }
}